TITLE: Your title goes here
DATA: FILE = "Mplus_files_Results/tso_long_trunc_n50_i4_nt15_na0.dat";
VARIABLE: 
NAMES = subjn time y1 y2 y3 y4; 
MISSING=.;
USEVAR = y1
y2
y3
y4 ;
CLUSTER = subjn ;

ANALYSIS:
TYPE = TWOLEVEL ;
ESTIMATOR = BAYES ;
BITERATIONS = (5000) ;

PROCESSORS = 2 ;

MODEL:

%WITHIN%

! Latent state residual
eta BY y1@1
y2 
y3 
y4   (&1) ;

! Autoregressive effect on the latent state residual
eta ON eta&1 ;

! Latent errors and latent state variances
y1(o_err1) ;
y2(o_err2) ;
y3(o_err3) ;
y4(o_err4) ;
eta(s_var) ;

%BETWEEN%

! Latent trait-indicator variables
theta1 BY y1@1 ;
theta2 BY y2@1 ;
theta3 BY y3@1 ;
theta4 BY y4@1 ;

! Intercepts
[y1*] ;
[y2*] ;
[y3*] ;
[y4*] ;

! Fixing means of latent trait-indicator variables at 0
[theta1@0] ;
[theta2@0] ;
[theta3@0] ;
[theta4@0] ;

! Fixing level-2 variances at 0 and latent trait-indicator variances
y1@0.001;
y2@0.001;
y3@0.001;
y4@0.001;
theta1(t_var1) ;
theta2(t_var2) ;
theta3(t_var3) ;
theta4(t_var4) ;

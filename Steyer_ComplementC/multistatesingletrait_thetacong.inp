TITLE: Your title goes here
DATA: FILE = "multistate.dat";
VARIABLE: 
NAMES = y11 y21 y12 y22; 
MISSING=.;

MODEL:

! labels of parameters are in parenthesis
! state variables congenericity assumption
eta1 by y11@1
y21* ( la211 ) ;

eta2 by y12@1
y22* ( la221 ) ;

! trait variable essential - equivalence assumption

theta by eta1@1 eta2 (la0) ;

!intercepts

[ eta1@0 eta2 * theta* ] ;
[y11@0 y12@0 ] ;

! variances of states and traits
eta1 ( varzeta1 ) ;
eta2 ( varzeta2 ) ;
theta ( vartheta ) ;

! residuals of observed variables
y11 ( vareps11 ) ;
y21 ( vareps21 ) ;
y12 ( vareps12 ) ;
y22 ( vareps22 ) ;

MODEL CONSTRAINT:
NEW( rely11 rely21 rely12 rely22 ) ;
NEW( spey11 spey21 spey12 spey22 ) ;
NEW( cony11 cony21 cony12 cony22 ) ;

rely11 = ( vartheta + varzeta1 ) / ( vartheta + varzeta1 + vareps11 ) ;
rely21 = ( la211 ^2* vartheta + la211 ^2* varzeta1 ) /
( la211 ^2* vartheta + la211 ^2* varzeta1 + vareps21 ) ;
rely12 = ( vartheta + varzeta2 ) / ( vartheta + varzeta2 + vareps12 ) ;
rely22 = ( la221 ^2* vartheta + la221 ^2* varzeta2 ) /
( la221 ^2* vartheta + la221 ^2* varzeta2 + vareps22 ) ;

spey11 = varzeta1 / ( vartheta + varzeta1 + vareps11 ) ;
spey21 = la211 ^2* varzeta1 / ( la211 ^2* vartheta + la211 ^2* varzeta1 + vareps21 ) ;
spey12 = varzeta2 / ( vartheta + varzeta2 + vareps12 ) ;
spey22 = la221 ^2* varzeta2 / ( la221 ^2* vartheta + la221 ^2* varzeta2 + vareps22 ) ;

cony11 = vartheta / ( vartheta + varzeta1 + vareps11 ) ;
cony21 = la211 ^2* vartheta / ( la211 ^2* vartheta + la211 ^2* varzeta1 + vareps21 ) ;
cony12 = vartheta / ( vartheta + varzeta2 + vareps12 ) ;
cony22 = la221 ^2* vartheta / ( la221 ^2* vartheta + la221 ^2* varzeta2 + vareps22 ) ;